# shellcheck disable=SC2164,SC2154
# Maintainer: Vicki Pfau <vi@endrift.com>
# Maintainer: Jeremy Whiting <jeremy.whiting@collabora.com>

pkgname=steamos-manager
# _srctag=v25.4.1.sk3
pkgver=25.12.0.dev1.r0.g305b951
pkgrel=1
pkgdesc='SteamOS Manager daemon for running various system management tasks'
arch=('x86_64')
url='https://store.steampowered.com/steamos/'
license=('MIT')
depends=('dbus'
		 'libspeechd'
		 'speech-dispatcher'
         'systemd'
         'wireless_tools')  # For iwconfig
optdepends=('jupiter-hw-support: jupiter support')  # Needed for jupiter-get-als-gain, jupiter-biosupdate, steamos-format-device, steamos-trim-devices
makedepends=('git' 'cargo' 'clang')
# _commit=ff62c8344c3dabb01388f2d8f711bf018833132d
source=("$pkgname::git+https://github.com/honjow/$pkgname.git#branch=dev")
sha256sums=('SKIP')
replaces=('ds-inhibit')

pkgver() {
  cd "${pkgname}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//'
}

prepare() {
	export RUSTUP_TOOLCHAIN=stable
	cd "$pkgname"
	local src
	for src in "${source[@]}"; do
		[[ $src = *.patch ]] || continue
		patch -p1 < "${srcdir}"/$src
	done
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
	cd "$pkgname"
	make build
}

package () {
	cd "$pkgname"
	make install DESTDIR="${pkgdir}"

	install -d -m0755 "$pkgdir/usr/lib/systemd/user/gamescope-session.service.wants/"
	ln -s ../steamos-manager.service "$pkgdir/usr/lib/systemd/user/gamescope-session.service.wants/"

	install -d -m0755 "$pkgdir/usr/lib/systemd/user/gamescope-session-plus@steam.service.wants/"
    ln -s ../steamos-manager.service "$pkgdir/usr/lib/systemd/user/gamescope-session-plus@steam.service.wants/"
}
