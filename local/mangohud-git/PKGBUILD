# Maintainer: Darvin Delgado <dnmodder at gmail dot com>

_pkgname=MangoHud
pkgname=mangohud-git
pkgver=0.8.3.r6.gd7654eb
pkgrel=1
pkgdesc="A Vulkan overlay layer for monitoring FPS, temperatures, CPU/GPU load and more"
url='https://github.com/flightlessmango/MangoHud'
license=('MIT')
arch=('x86_64')
makedepends=(
    'appstream'
    'cmocka'
    'dbus'
    'git'
    'glslang'
    'libxnvctrl'
    'libxrandr'
    'meson'
    'nlohmann-json'
    'python-mako'
    'vulkan-headers'
)
depends=(
    'gcc-libs'
    'glew'
    'glfw'
    'glibc'
    'hicolor-icon-theme'
    'libglvnd'
    'libx11'
    'libxkbcommon'
    'python'
    'python-matplotlib'
    'python-numpy'
    'sh'
    'wayland'
)
optdepends=(
    'dbus: mangohudctl'
    'libxnvctrl: NVIDIA GPU metrics on older GPUs'
    'vulkan-icd-loader: Vulkan support'
)
provides=('mangohud' 'mangoapp')
replaces=('mangoapp')
conflicts=('mangohud' 'mangohud-common-git' 'mangoapp')
_commit=10c3a130296c10477e54ddbdb6b7f819fd224edb
source=(
    "$_pkgname::git+$url#commit=$_commit"
    0001-feat-display-battery-charging-power.patch
    0002-Add-support-for-more-handheld-devices-to-fan-detecti.patch
    0003-amdgpu-add-cpu-temperature-fallback-for-gpu_metrics-.patch
    )
sha256sums=('SKIP'
            '4cc138237ba6807ed8cb6cdf19913af07fb8ff3712d77a90124f1b2a93fe4fa8'
            'b911370a98080af35ec4c80a4390a4bcad97b91aeccd84a657c3e25abea6b004'
            'a2139715a94a10bbc167bc914a8a9f4707b07d484fa3a51010c37f303c8f97de')

pkgver() {
    cd $_pkgname
    git describe --tags --long --abbrev=7 | sed 's/^v//;s/-rc[0-9]\+-/-/;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd ${srcdir}/${_pkgname}

    local src
    for src in "${source[@]}"; do
        [[ $src = *.patch ]] || continue
        patch -p1 < "${srcdir}"/$src
    done
}

build() {
    arch-meson "$_pkgname" build \
        -Dmangoapp=true \
        -Dmangohudctl=true \
        --wrap-mode=default

    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"

    install -Dm 0644 "$_pkgname/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
}
