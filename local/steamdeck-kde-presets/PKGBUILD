# Maintainer: SkorionOS Team
pkgname=steamdeck-kde-presets
pkgver=0.29
pkgrel=1
pkgdesc="KDE Presets from Valve's SteamOS 3.0"
arch=('any')
url="https://github.com/SkorionOS/skorionos"
license=('GPL2')
conflicts=('steameck-gnome-presets')
source=("https://gitlab.com/evlaV/${pkgname}/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('SKIP')

package() {
    cd "$srcdir/${pkgname}-${pkgver}"

    # Create directories
    install -dm755 "$pkgdir/usr/share"
    install -dm755 "$pkgdir/usr/bin"
    install -dm755 "$pkgdir/usr/lib"
    install -dm755 "$pkgdir/etc"

    # Copy main files
    cp -rv usr/share/* "$pkgdir/usr/share/"
    cp -rv usr/bin/* "$pkgdir/usr/bin/"
    cp -rv usr/lib/* "$pkgdir/usr/lib/"
    cp -rv etc/* "$pkgdir/etc/"

    # Handle icon renaming
    mv "$pkgdir/usr/share/icons/hicolor/scalable/places/distributor-logo-steamdeck.svg" \
        "$pkgdir/usr/share/icons/hicolor/scalable/places/steamdeck.svg"

    # Remove unneeded files
    rm -f "$pkgdir/etc/sddm.conf.d/steamdeck.conf"
    rm -f "$pkgdir/usr/share/icons/hicolor/scalable/places/distributor-logo.svg"
    rm -f "$pkgdir/etc/xdg/autostart/steam.desktop"
    rm -f "$pkgdir/usr/share/applications/org.mozilla.firefox.desktop"
    rm -f "$pkgdir/etc/profile.d/kde.sh"
    rm -f "$pkgdir/etc/xdg/kcm-about-distrorc"
    rm -f "$pkgdir/etc/X11/Xsession.d/50rotate-screen"
    rm -f "$pkgdir/usr/share/plasma/look-and-feel/com.valve.vapor.desktop/contents/splash/images/deck_logo.svgz"
    rm -f "$pkgdir/usr/share/plasma/look-and-feel/com.valve.vgui.desktop/contents/splash/images/deck_logo.svgz"
    rm -f "$pkgdir/etc/xdg/autostart/jupiter-plasma-bootstrap.desktop"
    rm -f "$pkgdir/usr/bin/jupiter-plasma-bootstrap"


    # Handle kio servicemenus
    install -dm755 "$pkgdir/usr/share/kio/servicemenus"
    if [ -f "$pkgdir/usr/share/kservices5/ServiceMenus/steam.desktop" ]; then
        mv "$pkgdir/usr/share/kservices5/ServiceMenus/steam.desktop" \
            "$pkgdir/usr/share/kio/servicemenus/steam.desktop"
        rm -rf "$pkgdir/usr/share/kservices5"
    fi
}
